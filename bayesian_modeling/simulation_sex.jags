model{
   # likelihood
    for (i in 1:n_obs){
    HAT[i] ~ dnorm(mu_observed[i], tau_error) # adding gps-specific error
    mu_observed[i] <- mu_bias + (HAT_index[i]-1)*real_alt[i] # on average, the observed altitude should be the real altitude + the average bias
    real_alt[i] ~ dnorm(mu_flight[i], tau_flight[i]) T(0,) # distribution of real flight altitudes
    mu_flight[i] <- mu_flight_intercept + sex[i]*mu_flight_beta
    tau_flight[i] <- pow(sigma_flight[i], -2)
    sigma_flight[i] <- sigma_flight_intercept + sex[i]*sigma_flight_beta
    }
    
    # priors
    for (k in 1:n_obs){
    HAT_index[k] ~ dcat(c(0.50, 0.50)) # HAT_index can be either 1 or 2
    }
   
    mu_bias ~ dnorm(0, 0.001) #negative values allowed
    # mu_flight ~ dnorm(330, 0.000001) T(0,) #negative values not allowed
    mu_flight_intercept ~ dnorm(0, 0.000001) T(0,) #negative values not allowed
    mu_flight_beta ~ dnorm(0, 0.000001)

    tau_error <- pow(sigma_error, -2) #precision
    sigma_error ~ dunif(0, 2500) #standard deviation
    
    sigma_flight_intercept ~ dnorm(0, 0.000001) T(0,) #standard deviation
    sigma_flight_beta ~ dnorm(0, 0.000001)
}  
    
    